link str_util

procedure main()
    local input := read(open("input.txt"))
    write("Part 1: ", part1(input), "\nPart 2: ", part2(input))
end

procedure part1(input)
    local toggle := &null, memory := [], fileNum := 0, valid := 0, sum1 := 0, c, rightPtr, d, pos
    every c := !input do
        if \toggle then {
            every !numeric(c) do put(memory, ".")
            toggle := &null
        } else {
            every !numeric(c) do put(memory, fileNum)
            valid +:= c
            toggle := 1
            fileNum +:= 1
        }
    rightPtr := *memory
    every pos := !valid do {
        d := memory[pos] 
        if d == "." then {
            while memory[rightPtr] == "." do rightPtr -:= 1
            d := memory[rightPtr]
            rightPtr -:= 1
        }
        sum1 +:= (pos - 1) * d
    }
    return sum1
end

record Pair(fileNum, size)

procedure part2(input)
    local toggle := &null, memory := [], fileNum := 0, sum2 := 0, c, rightPtr, pos, pair
    every c := !input do {
        if \toggle then {
            put(memory, Pair(".", c))
            toggle := &null
        } else {
            put(memory, Pair(fileNum, c))
            toggle := 1
            fileNum +:= 1
        }
    }
    rightPtr := *memory
    while memory[rightPtr].fileNum == "." do rightPtr -:= 1 
    while rightPtr > 0 do {
        every i := !(rightPtr - 1) do {
            if memory[i].fileNum ~== "." | memory[i].size < memory[rightPtr].size then next
            if memory[i].size = memory[rightPtr].size then memory[i] := memory[rightPtr]
            else { 
                memory[i].size -:= memory[rightPtr].size
                insert(memory, i, memory[rightPtr])
                rightPtr +:= 1
            }
            memory[rightPtr] := Pair(".", memory[rightPtr].size)
            break
        }
        rightPtr -:= 1
        while rightPtr > 0 & memory[rightPtr].fileNum == "." do rightPtr -:= 1 
    }
    pos := 0
    every pair := !memory do {
        if pair.fileNum == "." then {
            pos +:= pair.size
            next
        }
        every !numeric(pair.size) do {
            sum2 +:= pos * pair.fileNum
            pos +:= 1
        }
    }
    return sum2
end
