link str_util

procedure main()
    local input := read(open("input.txt")), counts := table(0)
    every counts[util::genFields(input, ' ')] +:= 1
    write("Part 1: ", iter(25, counts), "\nPart 2: ", iter(75, counts))
end

procedure iter(times, counts)
    local newCounts, k, v, result
    every !times do {
        newCounts := table(0)
        every k := key(counts) do {
            v := counts[k]
            if k = 0 then newCounts[1] +:= v
            else if *k % 2 = 0 then
                every newCounts[numeric(k[1 : *k / 2 + 1] | k[*k / 2 + 1 : 0])] +:= v
            else newCounts[k * 2024] +:= v
        }
        counts := newCounts
    }
    every (result := 0) +:= !counts
    return result
end
