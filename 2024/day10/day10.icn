procedure main()
    local input := open("input.txt"), lines := [], sum1 := 0, sum2 := 0, i, j, rec
    while put(lines, read(input))
    every i := !*lines do every j := !*lines[i] do 
        if lines[i, j] = 0 then {
            rec := recur(lines, i, j)
            sum1 +:= *set(rec)
            sum2 +:= *rec
        }
    write("Part 1: ", sum1, "\nPart 2: ", sum2)
end

procedure recur(grid, i, j)
    local curr := grid[i, j] 
    if curr = 9 then return [i || " " || j]
    return {
        ((\grid[i + 1, j] = curr + 1 & recur(grid, i + 1, j)) | []) |||
        ((\grid[i - 1, j] = curr + 1 & recur(grid, i - 1, j)) | []) |||
        ((\grid[i, j + 1] = curr + 1 & recur(grid, i, j + 1)) | []) |||
        ((\grid[i, j - 1] = curr + 1 & recur(grid, i, j - 1)) | [])
    }
end
